apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'signing'
apply plugin: 'maven'

version '0.1.0-SNAPSHOT'

mainClassName = 'org.dsa.iot.dummy.Main'
sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.iot-dsa'

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    // DSLink
    compile 'org.iot-dsa:dslink:0.17.2'
    compile 'org.iot-dsa:commons:0.17.2'
    compile 'org.iot-dsa:historian:0.17.2'


    compile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.5.2'
}

apply plugin: 'checkstyle'
checkstyle {
    configFile = file("${project.rootDir}/checkstyle/checkstyle.xml")
    toolVersion = '6.17'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

applicationDistribution.from new File(project.projectDir, "/dslink.json")

ext {
    if (!project.hasProperty("ossrhUsername"))
        ossrhUsername = ""
    if (!project.hasProperty("ossrhPassword"))
        ossrhPassword = ""
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name = 'Dummy DSLink'
                artifactId = 'dummy'
                description = 'Dummy DSLink for testing purposes.'
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging = 'jar'
                url = 'http://iot-dsa.org'

                scm {
                    connection = 'scm:git:https://github.com/IOT-DSA/dslink-java-dummy.git'
                    developerConnection = 'scm:git:git@github.com:IOT-DSA/dslink-java-dummy.git'
                    url = 'https://github.com/IOT-DSA/dslink-java-dummy'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'pacane'
                        name = 'Joel Trottier-Hebert'
                        email = 'j.hebert@dglogik.com'
                    }
                }
            }
        }
    }
}
